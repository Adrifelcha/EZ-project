
model {
          ####### Priors
          drift_mu ~ dnorm(0,1)   # Baseline
          drift_lambda ~ dgamma(2,1)
          drift_sigma = pow(drift_lambda, -0.5)
          for(i in 1:4){
              gamma[i] ~ dnorm(0,1)   
          }      
          
          for(j in 1:5){
              drift_pred[j] = drift_mu + X[j]*(gamma[1]*Y[j]+gamma[2]*Z[j]+gamma[3]*Y[j]*Z[j]) + (1-X[j])*gamma[4]
          }
          
          ####### Sampling model
          for (k in 1:length(nTrials)) {
              # Person-by-condition parameters for DM parameters
              bound[k] ~ dgamma(2,1)
              nondt[k] ~ dexp(1)
              drift[k] ~ dnorm(drift_pred[cond[k]],drift_lambda)
      
              # Forward equations from EZ Diffusion
              ey[k]  = exp(-bound[k] * drift[k])
              Pc[k]  = 1 / (1 + ey[k])
              PRT[k] = 2 * pow(drift[k], 3) / bound[k] * pow(ey[k] + 1, 2) / (2 * -bound[k] * 
                       drift[k] * ey[k] - ey[k] * ey[k] + 1)
              MDT[k] = (bound[k] / (2 * drift[k])) * (1 - ey[k]) / (1 + ey[k])
              MRT[k] = MDT[k] + nondt[k]
      
              # Sampling distributions for summary statistics
              correct[k] ~ dbin(Pc[k], nTrials[k])
              varRT[k]   ~ dnorm(1/PRT[k], 0.5*(nTrials[k]-1) * PRT[k] * PRT[k])
              meanRT[k]  ~ dnorm(MRT[k], PRT[k] * nTrials[k])
            }
}
